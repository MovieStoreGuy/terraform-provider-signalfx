---
page_tile: "Splunk Observability Cloud - signalfx_dimension_values
description: |-
    This data sources allows for obtaining a list of dimension values by on query provided.
---

# Data Source: signalfx_dimension_values

This data sources allows for obtaining a list of dimension values by on query provided.

# Examples Usage

```terraform
resource "signalfx_dashboard_group" "mydashboardgroup0" {
  name        = "My team dashboard group"
  description = "Cool dashboard group"
}

data "signalfx_dimension_values" "hosts" {
  query = "key:host"
}

resource "signalfx_time_chart" "host_charts" {
  for_each = toset(data.signalfx_dimension_values.hosts.values)

  name = "CPU Total Idle ${each.value}"

  plot_type         = "ColumnChart"
  axes_include_zero = true
  color_by          = "Metric"

  program_text = <<-EOF
A = data("cpu.idle", filter('host', '${each.key}').publish(label="CPU")
        EOF
}

resource "signalfx_dashboard" "mydashboard1" {
  name            = "My Dashboard"
  dashboard_group = signalfx_dashboard_group.mydashboardgroup0.id

  time_range = "-30m"

  grid {
    chart_ids = toset([for v in signalfx_time_chart.host_charts : v.id])
    width     = 3
    height    = 1
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `query` (String) Acepts a query string that allows for defining a key value deintion, wild card matching on values, or where the dimension value exists. Refer to https://dev.splunk.com/observability/reference/api/metrics_metadata/latest#endpoint-retrieve-dimensions-query for more details

### Optional

- `limit` (Number) This allows you to define how many dimensions are returned as the values output.
- `order_by` (String)

### Read-Only

- `id` (String) The ID of this resource.
- `values` (List of String) List of all the match dimension values that the provided query, ordered by order_by field

